defpackage slm/file-utils:
  import core
  import core/parsed-path

  import maybe-utils

  import slm/utils
  import slm/string-utils

public defn open (f: (FileOutputStream) -> False, path: String, append?: True|False) -> False:
  val file = FileOutputStream(path, append?)
  try: f(file)
  finally: close(file)

public defn open (f:(FileInputStream -> ?), path:String) -> ? :
  val file = FileInputStream(path)
  try: f(file)
  finally: close(file)

doc: \<DOC>
Convert Windows Backlash Path to Forward Slash Variant

This is the reverse of the `norm-path` function in lbstanza core.
This function is intended to help convert windows backslash paths
into a consistent format (ie, linux/mac OS-X path format).
<DOC>
public defn un-norm-path (path:String) -> String :
  #if-defined(PLATFORM-WINDOWS):
    defn convert-backslash-to-forward (path:String) -> String :
      replace(path, "\\", "/")
    convert-backslash-to-forward(path)
  #else:
    path

lostanza val GET-CWD-BUFF-SIZE: long = 0x1000L
extern getcwd: (ptr<byte>, long) -> ptr<byte>

lostanza defn int-get-cwd () -> ref<String>:
  val buff = call-c clib/malloc(GET-CWD-BUFF-SIZE)
  call-c getcwd(buff, GET-CWD-BUFF-SIZE)
  val ret = String(buff)
  call-c clib/free(buff)
  return ret

public defn get-cwd () -> String :
  un-norm-path $ int-get-cwd()

public defn base-name? (path: String) -> Maybe<String>:
  path
    $> entries{parse-path(_)}
    $> last?
    $> map{_, {[_]}}
    $> map{_, ParsedPath}
    $> map{_, to-string}

public defn create-empty-file (path: String) -> False:
  spit(path, "")

public defn slurp-stream (s: InputStream) -> String:
  val buffer = StringBuffer()
  defn* loop () :
    match(get-char(s)) :
      (c:Char) :
        add(buffer, c)
           loop()
      (c:False) :
        to-string(buffer)
  loop()

public defn delete-files-in-dir (path: String) -> False:
  label<False> return:
    if not file-exists?(path):
      return(false)

    val dir-path = parse-path(path)
    for file in dir-files(path) do:
      val file-path = to-string $ relative-to-dir(dir-path, file)
      if file-type(file-path, false) is DirectoryType:
        delete-recursive(file-path)
      else:
        delete-file(file-path)

public defn delete-file-if-exists (path: String) -> False:
  try:
    delete-file(path)
  catch (e: FileDeletionError):
    false
