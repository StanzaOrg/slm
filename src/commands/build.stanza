defpackage slm/commands/build:
  import core
  import collections
  import arg-parser
  import core/parsed-path

  import maybe-utils
  import semver
  import term-colors
  import toml

  import slm/dep-types
  import slm/dependencies
  import slm/file-utils
  import slm/logging
  import slm/process-utils
  import slm/flags
  import slm/toml
  import slm/utils
  import slm/stanza-utils
  import slm/platforms


defn get-build-target (cmd-args:CommandArgs) -> Tuple<String> :
  val arg = args(cmd-args)
  switch(length(arg)):
    1:
      debug("Target: '%_'" % [arg[0]])
      [arg[0]]
    0: []
    else:
      error("Invalid Number of Targets: %_" % [length(arg)])

public defn build (cmd-args:CommandArgs) -> False:
  val verbose =  get?(cmd-args, "verbose", false)
  if verbose:
    slm/flags/debug? = true

  val force-build = get?(cmd-args, "force", false)
  val build-args = get?(cmd-args, "-", [])
  val targ? = get-build-target(cmd-args)

  val cfg = parse-project-toml(SLM_TOML_NAME)

  ; This flag indicates to all dependencies that we are building
  ;  in static library mode to be able to compile in the build command.
  set-env("SLM_BUILD_STATIC", "1")

  ; Binary Variable that is used in both build/repl for scripts
  ;   that want to differentiate that way.
  ;   This is primarily added for 'conan' because it wants:
  ;   '--shared=(True|False)'
  set-env("SLM_BUILD_DYNAMIC_LIB", "False")

  val platform = get-platform()
  set-env("SLM_PLATFORM", platform)

  debug("calling slm/dependencies/fetch-and-sync")
  val dependencies = slm/dependencies/fetch-and-sync(cfg, force-build)

  debug("calling get-cwd")
  val slm-dir = path-join(get-cwd(), SLM_DIR)
  set-env("SLM_DIR", slm-dir)

  val stanza-exe = get-stanza-exe(compiler?(cfg))
  debug("with stanza='%_'" % [stanza-exe])
  debug("with build args '%,'" % [build-args])

  ; In general - we want all of the options for the project to be
  ;  captured in the `stanza.proj` file - not as options passed to this command.
  ;  Otherwise, the user will have to remember to pass `-pkg pkgs` on the command
  ;  line to the `stanza build` if they want to run the compiler directly instead
  ;  of through `slm`
  val args = to-tuple $ cat-all([[stanza-exe, "build"], targ?, build-args])
  val vStr =  version(cfg)
  debug("Build Version: %_" % [vStr])
  val env-vars = ["SLM_BUILD_VERSION" => vStr]
  debug("Stanza: %," % [args])
  ProcessBuilder(args)
    $> with-env-vars{_, env-vars}
    $> build
    $> wait-process-throw-on-nonzero{_, "build failed!"}

  false

val BUILD-MSG = \<MSG>
The 'build' command will manage syncing dependencies and then
building the project using the 'stanza build' process.

The user can pass arguments to the 'stanza build' process
by using the '-' flag argument.

Example:

$> slm build -- -flags TESTING

---------------------------
Package Version Propagation
---------------------------

In order for the package's source code to know what its current
version number is, this tool defines 'SLM_BUILD_VERSION' with the
'version' string value from the 'slm.toml' file. This environment
variable is defined for the context of the `stanza build` process.

This allows the package's stanza code to use '#env-var(SLM_BUILD_VERSION)'
to access this version string and compile it into the binary or library
that is being constructed.

------------------------------
Static Dependency Construction
------------------------------

With the task dependencies, external programs can be used to build
the artifacts needed to load functionality from a static C library.
To support this, the 'build' command defines the environment
variable 'SLM_BUILD_STATIC=True'. This environment variable is intended
to indicate to any of the task dependencies that a static library
is needed (as opposed to a shared library, like a *.dll)

Additionally, the 'build' command defines the environment variable
'SLM_BUILD_DYNAMIC_LIB=False'. This is the same env-var used by the 'repl'
command which makes it easier for scripts to just reference one variable.

The 'slm' binary will also define the 'SLM_PLATFORM' environment
variable during the dependendency resolution process. This environment
variable will contain one of the following strings:
  1.  windows
  2.  linux
  3.  os-x

----------------------------
SLM_DIR
----------------------------

This command will define the `SLM_DIR` environment variable and
it will point at the `.slm` directory for the top-level project
being built. This will give subsequent child builds access to
a known directory from which to find other dependencies.

<MSG>

val BUILD-MSG-ARG = \<MSG>

The build command takes an optional argument which is the target in the
stanza.proj file to build. Without this optional argument, the command
follows the `stanza build` command's logic and compiles the `main` target.

Running:

```
$> slm build target
```

Is effectively the same as running:

```
$> stanza build target
```

But with all the dependencies handled.

<MSG>

val VERBOSE-FLAG = \<MSG>
Generate verbose output from build.
<MSG>

val FORCE-FLAG = \<MSG>
This flag instructs the build system to ignore any stanza
version compatibility checks and attempt to build this project
regardless.
<MSG>

val BUILD-ARGS-FLAG = \<MSG>
To pass additional build arguments directly to the
stanza build invokation, the user can use the '--'
sequence. All arguments after the '--' will be
passed directly to the build.
<MSG>

public defn setup-build-cmd () -> Command :
  val buildFlags = [
    Flag("verbose", ZeroFlag, OptionalFlag, VERBOSE-FLAG)
    Flag("force", ZeroFlag, OptionalFlag, FORCE-FLAG),
    Flag("-", AllRemainingFlag, OptionalFlag, BUILD-ARGS-FLAG)

  ]
  Command("build", ZeroOrOneArg, BUILD-MSG-ARG, buildFlags, BUILD-MSG, build)