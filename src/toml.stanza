defpackage slm/toml:
  import core
  import collections

  import semver
  import maybe-utils
  import toml

  import slm/dep-types
  import slm/logging
  import slm/utils
  import slm/task
  import slm/errors
  import slm/stanza-utils
  import slm/file-utils

public defstruct SlmToml:
  name: String
  version: String
  compiler?: Maybe<String>
  stanza-version?: Maybe<String>
  dependencies: HashTable<String, Dependency> with: (updater => sub-deps)

public defn SlmToml (
  name: String
  version: String
  deps: Tuple<KeyValue<String, Dependency>>
  --
  compiler? = ?
  stanza-version? = ?
  ):
  val deps* = to-hashtable<String, Dependency>(deps)
  SlmToml(name, version, compiler?, stanza-version?, deps*)

doc: \<DOC>
Extract the Semantic Version for the Stanza Version specification

@return If None - it means that no version spec is required.
If One - then this value is the semantic version required by this
configuration file.
<DOC>
public defn get-stanza-version? (cfg:SlmToml) -> Maybe<SemanticVersion> :
  match(stanza-version?(cfg)):
    (x:None): x
    (raw-str?:One<String>):
      val raw-str = value(raw-str?)
      val vers? = parse-semver(raw-str)
      One $ value-or-throw(vers?, InvalidVersionError(raw-str))

doc: \<DOC>
Parse the project level TOML file

This extracts the configuration and does some setup
for the stanza version checking. This is primarily a
helper function to make sure the `get-proj-stanza-version!`
gets called on valid data for the project.
<DOC>
public defn parse-project-toml (path:String -- env-sub-enable:True|False = true) -> SlmToml :
  val cfg = parse-slm-toml-file(path, env-sub-enable = env-sub-enable)
  get-proj-stanza-version!(compiler?(cfg))
  cfg

doc: \<DOC>
Parse the SLM TOML configuration file

@param f Input data in the form of a stream. This makse testing
the implementation easier.
@param env-sub-enable Allow environment variable substitutions.
This will replace strings like `{HOME}` with environment variable
values if they exist.
<DOC>
public defn parse-slm-toml-file (file:String -- env-sub-enable:True|False = true) -> SlmToml:
  val table = file $> parse-file $> table

  val name = table["name"] as String
  debug("parse-slm-toml: name: \"%_\"" % [name])

  val version = table["version"] as String
  debug("parse-slm-toml: version: \"%_\"" % [version])

  val compiler? = get-str?(table, "compiler")
  debug("parse-slm-toml: compiler?: \"%_\"" % [compiler?])

  val stanza-version? = get-str?(table, "stanza-version")
  debug("parse-slm-toml: stanza-version?: \"%_\"" % [stanza-version?])

  val dependencies = to-hashtable<String, Dependency> $
    for [name, specifier] in pairs(table["dependencies"] as TomlTable) seq:
      debug("parse-slm-toml: dependency name: \"%_\"" % [name])
      debug("parse-slm-toml: dependency specifier: \"%_\"" % [specifier])
      name => match(specifier):
        (legacy:String):
          debug("parse-slm-toml: calling parse-git-dependency(\"%_\", \"%_\")" % [name, legacy])
          parse-git-dependency(name, legacy)
        (table:TomlTable):
          debug("parse-slm-toml: calling parse-dependency(\"%_\", %_, %_)" % [name, table, env-sub-enable])
          parse-dependency(name, table, env-sub-enable)

  SlmToml(name, version, compiler?, stanza-version?, dependencies)

public defstruct InvalidDependencyConfigError <: Exception :
  name:String
  msg:String|Printable

defmethod print (o:OutputStream, e:InvalidDependencyConfigError) :
  print(o, "Invalid Dependency[%_]: %_" % [name(e), msg(e)])

defn parse-dependency (name:String, table:TomlTable, env-sub-enable:True|False) -> Dependency :
  val gitAttr = get-str?(table, "git")
  val pathAttr = get-str?(table, "path")
  val pkgAttr = get-str?(table, "pkg")
  val taskAttr = get-table?(table, "task")
  val version = get-str?(table, "version")

  debug("parse-dependency: gitAttr: \"%_\"" % [gitAttr])
  debug("parse-dependency: pathAttr: \"%_\"" % [pathAttr])
  debug("parse-dependency: pkgAttr: \"%_\"" % [pkgAttr])
  debug("parse-dependency: taskAttr: \"%_\"" % [taskAttr])
  debug("parse-dependency: version: \"%_\"" % [version])

  match(pathAttr, gitAttr, pkgAttr, taskAttr):
    (p:One<String>, g:None, k: None, t:None): ; Path Dependency
      debug("parse-dependency: calling parse-path-dependency(\"%_\", ...)" % [name])
      parse-path-dependency(name, value(p), version, env-sub-enable)
    (p:None, g:One<String>, k: None, t:None): ; Git Dependency
      debug("parse-dependency: calling parse-git-dependency(\"%_\", ...)" % [name])
      parse-git-dependency(name, value(g), version)
    (p:None, g:None, k: One<String>, t:None): ; Pkg Dependency
      debug("parse-dependency: calling parse-pkg-dependency(\"%_\", ...)" % [name])
      parse-pkg-dependency(name, table, env-sub-enable = env-sub-enable)
    (p:None, g:None, k: None, t:One): ; Task Dependency
      debug("parse-dependency: calling parse-task-dependency(\"%_\", ...)" % [name])
      parse-task-dependency(name, table, env-sub-enable = env-sub-enable)
    (p:None, g:None, k: None, t:None): ; Invalid Dependency - no type specified
      throw $ InvalidDependencyConfigError(name, "Expected at least one of 'path', 'git', 'pkg', or 'task'")
    (p, g, k, t):
      throw $ InvalidDependencyConfigError(name, "The 'path', 'git', 'pkg', and 'task' keys are mutually exclusive. Your config has an invalid combination.")

doc: \<DOC>
Output the SlmToml config as a valid TOML file.
<DOC>
public defmethod write (o:OutputStream, cfg:SlmToml) :
  println(o, "name = \"%_\"" % [name(cfg)])
  println(o, "version = \"%_\"" % [version(cfg)])
  match(compiler?(cfg)):
    (x:One<String>): println(o, "compiler = \"%_\"" % [x] )
    (_:None): false
  println(o, "[dependencies]")
  for dep in values(dependencies(cfg)) do:
    println(o, dep)

public defn find-dependency (cfg:SlmToml, name:String) -> Maybe<Dependency> :
  val deps = dependencies(cfg)
  for dep-name in keys(deps) first :
    One(deps[name]) when name == dep-name else None()

doc: \<DOC>
Check if this SLM config's stanza requirement is met.

This function assumes that the stanza version has already
been acquired and that the `get-stanza-version!` function
will return a valid `SemanticVersion`
<DOC>
public defn is-stanza-compatible? (cfg:SlmToml) -> [True|False, String] :
  match(get-stanza-version?(cfg)):
    (x:None): [true, "No Stanza Version Requirement Specified"]
    (raw-spec:One<SemanticVersion>):
      val exp = value(raw-spec)
      val obs = get-stanza-version!()
      val msg = to-string("Expected Stanza Version: %_" % [exp])
      [compatible?(exp, obs), msg]
