defpackage slm/toml:
  import core
  import collections

  import maybe-utils
  import toml/file
  import toml/parser
  import toml/table
  import toml/value

  import slm/dependency
  import slm/logging
  import slm/utils

public defstruct SlmToml:
  name: String
  version: String
  compiler?: Maybe<String>
  dependencies: HashTable<String, Dependency>

public defn parse-slm-toml (path: String) -> SlmToml:
  val table = path $> parse-file $> table
  val name = table["name"] as String
  val version = table["version"] as String
  val compiler? = match(get?(table, "compiler")):
    (x:None): x
    (x:One<TomlValue>):  One(value(x) as String)
  val dependencies = to-hashtable<String, Dependency> $
    for [name, specifier] in pairs(table["dependencies"] as TomlTable) seq:
      name => match(specifier):
        (specifier: String):
          parse-git-dependency(name, specifier)
        (table: TomlTable):
          parse-path-dependency(name, table["path"] as String)
        (_):
          error("invalid slm.toml: '%_' is not a valid dependency specifier"
                % [specifier])

  SlmToml(name, version, compiler?, dependencies)
